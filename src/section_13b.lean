/-
Copyright (c) 2018 Sander Dahmen, Johannes H√∂lzl, Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sander Dahmen, Johannes H√∂lzl, Robert Y. Lewis

"On large subsets of ùîΩ‚Åø_q with no three-term arithmetic progression"
by J. S. Ellenberg and D. Gijswijt

This file establishes the asymptotic behavior of the cap set cardinality bound
proved in section_10_11_12.lean.
It corresponds to the second half of section 13 of our blueprint.
-/

import data.vector2 data.zmod.basic data.nat.modeq
import tactic.library_search
import
  section_10_11_12
  has_deriv


def iseg (q j) : finset (fin q) :=
finset.univ.filter (Œª k, k.val ‚â§ j)

lemma val_le_right_of_iseg {q j} {v : fin q} (h : v ‚àà iseg q j) : v.val ‚â§ j :=
(finset.mem_filter.1 h).2

lemma mem_iseg_of_le {q j} {v : fin q} (h : v.val ‚â§ j) : v ‚àà iseg q j :=
by simp [iseg, h]

section fix_q
parameter (q : ‚Ñï)

section cf_def
parameter (n : ‚Ñï)

def sf (j : ‚Ñï) : finset (vector (fin q) n) := finset.univ.filter (Œª f, (f.nat_sum = j))

parameter {n}

lemma mem_sf {j} {v : vector (fin q) n} (h : v.nat_sum = j) : v ‚àà sf j :=
finset.mem_filter.2 ‚ü®finset.mem_univ _, h‚ü©

lemma sf_sum {j : ‚Ñï} {f : vector (fin q) n} (h : f ‚àà sf j) : f.nat_sum = j :=
(finset.mem_filter.1 h).2

lemma sf_disjoint {j1 j2 : ‚Ñï} (h : j1 ‚â† j2) : sf j1 ‚à© sf j2 = ‚àÖ :=
finset.eq_empty_of_forall_not_mem $ Œª x hx, h $ by simp at hx; rw [‚Üêsf_sum hx.1, ‚Üêsf_sum hx.2]

def sf_lift_i (i : fin q) (sfj : finset (vector (fin q) n)) : finset (vector (fin q) (n+1)) :=
sfj.image $ vector.cons i

lemma sf_lift_i_sum {i : fin q} {j} {f} (hf : f ‚àà sf_lift_i i (sf j)) : f.nat_sum = i.val + j :=
by rcases finset.mem_image.1 hf with ‚ü®_, hg, hfg‚ü©; simp [hfg.symm, sf_sum hg]

lemma sf_lift_i_card (i : fin q) (sfj : finset (vector (fin q) n)) :
  (sf_lift_i i sfj).card = sfj.card :=
finset.card_image_of_inj_on $ Œª _ _ _ _, vector.cons_inj

lemma sf_lift_i_inter_empty  {i j : fin q} (h : i ‚â† j) (s1 s2 : finset (vector (fin q) n)) :
  sf_lift_i i s1 ‚à© sf_lift_i j s2 = ‚àÖ :=
finset.eq_empty_of_forall_not_mem $ Œª x hx,
  let ‚ü®hx1, hx2‚ü© := finset.mem_inter.1 hx,
      ‚ü®a1, hm1, ha1‚ü© := finset.mem_image.1 hx1,
      ‚ü®a2, hm2, ha2‚ü© := finset.mem_image.1 hx2 in
  h $ vector.cons_inj_left $ by rwa ‚Üêha2 at ha1

lemma mem_sf_lift_i (v) : v ‚àà sf_lift_i (vector.head v) (sf (v.nat_sum - (vector.head v).val)) :=
finset.mem_image.2 $
  ‚ü®v.tail, mem_sf $ eq.symm $ nat.sub_eq_of_eq_add $ by rw [‚Üêvector.nat_sum_head_tail], by simp‚ü©

parameter (n)
def sf_lift (j : ‚Ñï) : finset (vector (fin q) (n+1)) :=
finset.bind (iseg q j) (Œª i, sf_lift_i i (sf (j - i)))

def cf (j : ‚Ñï) : ‚Ñï := (sf j).card
parameter {n}

lemma sf_sum_mem_lift {j : ‚Ñï} {f : vector (fin q) (n+1)} (h : f ‚àà sf_lift j) : f.nat_sum = j :=
begin
  simp [sf_lift, sf_lift_i] at h,
  rcases h with ‚ü®k, ‚ü®hk, g, hg, hfg‚ü©‚ü©,
  suffices : k.val + (j - k.val) = j, {simpa [hfg.symm, sf_sum hg]},
  rw [nat.add_comm, nat.sub_add_cancel],
  apply val_le_right_of_iseg hk
end

end cf_def

lemma head_le_of_mem_sf {j n : ‚Ñï} : ‚àÄ {v : vector (fin q) (n+1)} (h : v ‚àà sf (n+1) j), v.head.val ‚â§ j
| p@‚ü®h::t,_‚ü© hmem :=
  have hsum : _, from vector.nat_sum_head_tail p,
  by rw sf_sum hmem at hsum; linarith

lemma sf_lift_succ (n j : ‚Ñï) : sf (n + 1) j = sf_lift n j :=
begin
  ext v, constructor,
  { intro hv,
    apply finset.mem_bind.2,
    use [v.head, mem_iseg_of_le (head_le_of_mem_sf hv)],
    rw ‚Üêsf_sum hv,
    apply mem_sf_lift_i },
  { intro hv, simp [sf_sum_mem_lift hv, sf] }
end

lemma sf_lift_card' (n j : ‚Ñï) :
  (sf_lift n j).card = (iseg q j).sum (Œª u, finset.card ((Œª i, sf_lift_i i (sf n (j - ‚Üëi))) u)) :=
finset.card_bind $ Œª x hx y hy hxy, sf_lift_i_inter_empty hxy _ _

lemma sf_lift_card (n j : ‚Ñï) : (sf_lift n j).card = (iseg q j).sum (Œª u, cf n (j - u)) :=
by simp only [sf_lift_card', sf_lift_i_card, cf]

lemma cf_zero_zero (h : q > 0) : cf 0 0 = 1 :=
finset.card_eq_one.2 ‚ü® vector.nil, finset.ext.2 $
  Œª v, ‚ü®Œª _, finset.mem_singleton.2 v.eq_nil, Œª hv, mem_sf (by rw finset.mem_singleton.1 hv; simp)‚ü© ‚ü©

lemma cf_zero_succ (j : ‚Ñï) : cf 0 (j + 1) = 0 :=
finset.card_eq_zero.2 $ finset.eq_empty_of_forall_not_mem $ Œª v hv, nat.succ_ne_zero j $ by simpa using sf_sum hv

lemma cf_one_lt {j : ‚Ñï} (hj : j < q) : cf 1 j = 1 :=
begin
  have hfs : finset.univ.filter (Œª f : vector (fin q) 1, f.nat_sum = j) = {‚ü®j, hj‚ü©::vector.nil},
  { ext v, simp, constructor,
    { rw vector.vec_one v,
      intro h,
      congr,
      rw fin.ext_iff,
      simpa using h },
    { intro hv, simp [hv] } },
  simp only [cf, sf, hfs], refl
end

lemma cf_one_ge {j : ‚Ñï} (hj : j ‚â• q) : cf 1 j = 0 :=
begin
  simp only [cf, sf],
  convert finset.card_empty,
  apply finset.eq_empty_of_forall_not_mem,
  intros x hx,
  cases finset.mem_filter.1 hx with _ hx,
  apply not_lt_of_ge hj,
  rw ‚Üêhx,
  apply vector.nat_sum_one
end

lemma cf_succ (n j : ‚Ñï) : cf (n + 1) j = (iseg q j).sum (Œª u, cf n (j - u)) :=
by rw [cf, sf_lift_succ, sf_lift_card]

lemma cf_mul (n j : ‚Ñï) : (finset.range (j + 1)).sum (Œª i, (cf 1 (j - i)) * cf (n + 1) i) = cf (n+2) j :=
have hfil : finset.range (j + 1) = (finset.range (j + 1)).filter (Œª k, j - k < q) ‚à™ (finset.range _).filter _,
  from eq.symm (finset.filter_union_filter_neg_eq _),
begin
  rw [cf_succ, hfil, finset.sum_union],
  { have hgeq : ((finset.range (j + 1)).filter (Œª k, ¬¨ j - k < q)).sum (Œª i, cf 1 (j - i) * cf (n + 1) i) = 0,
    { apply finset.sum_eq_zero,
      intros k hk,
      simp [cf_one_ge (le_of_not_gt (finset.mem_filter.1 hk).2)] },
    rw [hgeq, add_zero],
    symmetry,
    apply finset.sum_bij (Œª (a : fin q) _, j - a.val),
    { intros a ha,
      simp,
      refine ‚ü®nat.sub_lt_succ _ _, _‚ü©,
      rw nat.sub_sub_self (val_le_right_of_iseg ha),
      exact a.is_lt },
    { intros a ha,
      simp [nat.sub_sub_self (val_le_right_of_iseg ha), cf_one_lt a.is_lt], refl },
    { intros a a2 ha1 ha2 heq,
      apply (fin.ext_iff _ _).2,
      rwa ‚Üê nat.sub_eq_sub_iff; apply val_le_right_of_iseg; assumption },
    { intros b hb,
      simp at hb,
      use [‚ü®j-b, hb.2‚ü©, mem_iseg_of_le (nat.sub_le_self _ _)],
      simp [nat.sub_sub_self (nat.le_of_lt_succ hb.1)] } },
  { rw [finset.filter_not], simp }
end

def one_coeff_poly : polynomial ‚Ñï :=
(finset.range q).sum (Œª k, (polynomial.X : polynomial ‚Ñï) ^ k)

lemma one_coeff_poly_coeff {n : ‚Ñï} : one_coeff_poly.coeff n = if n < q then 1 else 0 :=
begin
  rw [one_coeff_poly],
  symmetry,
  have hlx : (Œª k : ‚Ñï, ((polynomial.X : polynomial ‚Ñï) ^k).coeff n) = (Œª k : ‚Ñï, if n = k then 1 else 0),
  { ext k, rw [polynomial.coeff_X_pow k n] },
  have hfr : finset.range q = (finset.range q).filter (Œª k, n = k) ‚à™ (finset.range q).filter (Œª k, n ‚â† k),
    from eq.symm (finset.filter_union_filter_neg_eq _),
  have hoeq : (finset.range q).sum (Œª k, ((polynomial.X : polynomial ‚Ñï)^k).coeff n) = if n < q then 1 else 0,
  { rw [hlx, hfr, finset.sum_union, ‚Üêadd_zero (ite _ _ _)],
    { congr,
      { by_cases hnq : n < q,
        { have hs : (finset.range q).filter (Œª k, n = k) = finset.singleton n,
          { ext a, simp, constructor,
            { rintros ‚ü®_, ha‚ü©, rw ha },
            { intro ha, rw ha, exact ‚ü®hnq, rfl‚ü© } },
          simp [hs, hnq] },
        { simp [hnq],
          apply finset.sum_eq_zero,
          intros x hx,
          exfalso,
          apply hnq,
          rw (finset.mem_filter.1 hx).2,
          apply finset.mem_range.1 (finset.mem_filter.1 hx).1 } },
      { apply finset.sum_eq_zero,
        intros _ hx,
        simp [(finset.mem_filter.1 hx).2] } },
    { apply finset.eq_empty_of_forall_not_mem,
      intros x hx,
      cases finset.mem_inter.1 hx with hx1 hx2,
      cases finset.mem_filter.1 hx1 with _ hxn,
      cases finset.mem_filter.1 hx2 with _ hxn2,
      exact hxn2 hxn }},
  rw ‚Üêhoeq,
  apply finset.sum_hom (Œª p, polynomial.coeff p n),
  apply_instance,
  convert is_add_monoid_hom.mk _,
  { convert is_add_hom.mk _,
    intros,
    apply polynomial.coeff_add },
  { apply polynomial.coeff_zero }
end

lemma one_coeff_poly_coeff_lt {n : ‚Ñï} (h : n < q): one_coeff_poly.coeff n = 1 :=
have he : _ := @one_coeff_poly_coeff n,
have 1 = (if n < q then 1 else 0), by split_ifs; refl,
by rw this; exact he

lemma one_coeff_poly_coeff_ge {n : ‚Ñï} (h : n ‚â• q): one_coeff_poly.coeff n = 0 :=
have he : _ := @one_coeff_poly_coeff n,
have 0 = (if n < q then 1 else 0), by simp [not_lt_of_ge h],
by rw this; exact he

lemma one_coeff_poly_eval‚ÇÇ_nonneg {Œ≤} [linear_ordered_semiring Œ≤] {f : ‚Ñï ‚Üí Œ≤} (hf : ‚àÄ z, f z ‚â• 0) {b : Œ≤} (hb : b ‚â• 0) :
  0 ‚â§ one_coeff_poly.eval‚ÇÇ f b :=
finset.zero_le_sum $ Œª x _, mul_nonneg (hf _) (pow_nonneg hb _)

theorem lemma_13_9 (hq : q > 0) : ‚àÄ n j : ‚Ñï, (one_coeff_poly ^ n).coeff j = cf n j
| 0 0 := by rw [pow_zero, polynomial.coeff_one, cf_zero_zero]; [refl, assumption]
| 0 (j+1) := by rw [pow_zero, polynomial.coeff_one, cf_zero_succ]; simp [show 0 ‚â† j + 1, from (nat.succ_ne_zero _).symm]
| 1 j :=
  begin
    by_cases h : j < q,
    { rw [pow_one, cf_one_lt h, one_coeff_poly_coeff_lt h] },
    { rw [pow_one, cf_one_ge (le_of_not_gt h), one_coeff_poly_coeff_ge (le_of_not_gt h)] }
  end
| (n+2) j := calc
  (one_coeff_poly ^ (n + 2)).coeff j
      = (one_coeff_poly * one_coeff_poly ^ (n + 1)).coeff j : rfl
  ... = _ : polynomial.coeff_mul_right _ _ _
  ... = (finset.range (j + 1)).sum (Œª i, one_coeff_poly.coeff (j - i) * cf (n + 1) i) : by congr; ext; rw lemma_13_9
  ... = (finset.range (j + 1)).sum (Œª i, (one_coeff_poly ^ 1).coeff (j - i) * cf (n + 1) i) : by rw pow_one
  ... = (finset.range (j + 1)).sum (Œª i, cf 1 (j - i) * cf (n + 1) i) : by congr; ext i; rw lemma_13_9
  ... = cf (n + 2) j : cf_mul _ _

parameter {q}
def vector_of_fin_fn : ‚àÄ {n} (v : fin n ‚Üí fin q), vector (fin q) n
| 0  v := vector.nil
| (n+1) v := v ‚ü®n, nat.lt_succ_self _‚ü© :: vector_of_fin_fn (Œª k : fin n, v ‚ü®k, nat.lt_succ_of_lt k.is_lt‚ü©)

lemma vector_of_fin_fn_nth : ‚àÄ n (v : fin (n+1) ‚Üí fin q) (i : fin (n+1)),
  (vector_of_fin_fn v).nth i = v ‚ü®n - i.val, nat.lt_succ_of_le (nat.sub_le_self _ _)‚ü©
| 0 v x :=
  have hx : x = ‚ü®0, zero_lt_one‚ü©, from (fin.ext_iff _ _).2 (nat.eq_zero_of_le_zero (nat.le_of_lt_succ x.is_lt)),
  by simp [vector_of_fin_fn, hx]; refl
| (n+1) v x :=
  begin
    rw [vector_of_fin_fn],
    by_cases hx : x.val = 0,
    { have hx' : x = 0, by simp [fin.ext_iff, hx, fin.val_zero],
      simp [hx', vector.nth_zero, fin.val_zero] },
    { have : x = fin.succ ‚ü®x.val - 1, _‚ü©,
      { apply fin.eq_of_veq,
        rw fin.succ_val,
        symmetry,
        apply nat.sub_add_cancel (nat.succ_le_of_lt (nat.pos_of_ne_zero hx)) },
      { rw [this, vector.nth_cons_succ, vector_of_fin_fn_nth],
        congr, simp, refl },
      { rw nat.sub_lt_right_iff_lt_add,
        apply x.is_lt,
        exact nat.succ_le_of_lt (nat.pos_of_ne_zero hx) }}
  end

def fin_fn_of_vector : ‚àÄ {n} (v : vector (fin q) n), fin n ‚Üí fin q
| 0 v i := fin_zero_elim i
| (n+1) v i := v.nth ‚ü®n - i.val, nat.lt_succ_of_le (nat.sub_le_self _ _)‚ü©

lemma vector_of_fin_fn_inv : ‚àÄ {n} (v : vector (fin q) n), vector_of_fin_fn (fin_fn_of_vector v) = v
| 0 v := vector.ext $ Œª k, fin_zero_elim k
| (k+1) v :=
  begin
    ext m,
    simp only [fin_fn_of_vector, vector_of_fin_fn_nth],
    congr,
    rw fin.ext_iff,
    apply nat.sub_sub_self,
    exact nat.le_of_lt_succ m.is_lt
  end

lemma sum_fin_fn_of_vector : ‚àÄ {n} (v : vector (fin q) n), fin.sum (‚Üë(fin_fn_of_vector v) : fin n ‚Üí ‚Ñï) = v.nat_sum
| 0 v := by simp [fin.sum_zero]
| (k+1) v :=
  begin
    rw [fin.sum_succ, vector.nat_sum_head_tail],
    congr,
    { simp [cast_fin_fn, fin_fn_of_vector], congr, apply vector.nth_zero },
    { convert sum_fin_fn_of_vector _,
      ext x,
      simp [cast_fin_fn, fin_fn_of_vector],
      cases k,
      { apply fin_zero_elim x },
      { rw [fin_fn_of_vector, vector.nth_tail],
        congr,
        rw nat.succ_sub; [refl, exact nat.le_of_lt_succ x.is_lt] } }
   end


lemma inv_in_range {d : ‚Ñö} (hd : 0 ‚â§ d) {n} {v : vector (fin q) n}
  (hv : v ‚àà (finset.range (int.nat_abs ‚åäd‚åã + 1)).bind (sf n)) :
  ‚Üë(fin.sum (‚Üë(fin_fn_of_vector v) : fin n ‚Üí ‚Ñï)) ‚â§ d :=
begin
  rw sum_fin_fn_of_vector,
  simp [-add_comm] at hv,
  rcases hv with ‚ü®a, ha, ha2‚ü©,
  replace ha2 := sf_sum ha2,
  have : 0 ‚â§ ‚åäd‚åã := le_floor.2 hd,
  calc (vector.nat_sum v : ‚Ñö) ‚â§ int.nat_abs ‚åäd‚åã :
      by rw ha2; exact nat.cast_le.2 (nat.succ_le_succ_iff.1 ha)
    ... = ((int.nat_abs ‚åäd‚åã : ‚Ñ§) : ‚Ñö) : by rw [‚Üê coe_coe]
    ... = _ : by rw [‚Üê int.eq_nat_abs_of_zero_le this]
    ... ‚â§ d : floor_le _
end

lemma nat_sum_vector_of_fin_fn : ‚àÄ {n} (v : fin n ‚Üí fin q), (vector_of_fin_fn v).nat_sum = fin.sum (‚Üëv : fin n ‚Üí ‚Ñï)
| 0 v := by simp [vector_of_fin_fn, fin.sum_zero]
| (n+1) v := by simp [vector_of_fin_fn, nat_sum_vector_of_fin_fn, fin.sum_succ]; refl


lemma vector_of_fin_fn_mem_bind {n} {d : ‚Ñö}
  {v : fin n ‚Üí fin q} (hv : ‚Üë(fin.sum (‚Üëv : fin n ‚Üí ‚Ñï)) ‚â§ d) :
  vector_of_fin_fn v ‚àà (finset.bind (finset.range (int.nat_abs ‚åäd‚åã + 1)) (sf n)) :=
begin
  apply finset.mem_bind.2,
  use fin.sum (‚Üëv : fin n ‚Üí ‚Ñï),
  split,
  { apply finset.mem_range.2,
    refine (nat.cast_lt.1 $ lt_of_le_of_lt hv _),
    rw [nat.cast_add, nat.cast_one],
    have hd : 0 ‚â§ ‚åäd‚åã := le_floor.2 (le_trans (nat.cast_le.2 (nat.zero_le _)) hv),
    have : (int.nat_abs ‚åäd‚åã : ‚Ñö) = ((int.nat_abs ‚åäd‚åã : ‚Ñ§) : ‚Ñö), { rw ‚Üê coe_coe },
    rw [this, ‚Üê int.eq_nat_abs_of_zero_le hd],
    exact lt_floor_add_one d },
  { apply mem_sf,
    apply nat_sum_vector_of_fin_fn }
end

lemma vector_of_fin_fn_inj : ‚àÄ {n} {v1 v2 : fin n ‚Üí fin q}, vector_of_fin_fn v1 = vector_of_fin_fn v2 ‚Üí v1 = v2
| 0 v1 v2 h := funext $ Œª x, fin_zero_elim x
| (n+1) v1 v2 h := funext $ Œª x,
  have (Œª k : fin n, v1 ‚ü®k, nat.lt_succ_of_lt k.is_lt‚ü©) = (Œª k : fin n, v2 ‚ü®k, nat.lt_succ_of_lt k.is_lt‚ü©),
    from vector_of_fin_fn_inj $ vector.cons_inj h,
  begin
    by_cases hx : x.val < n,
    { convert congr_fun this ‚ü®x.val, _‚ü©,
      repeat {simp [fin.ext_iff], refl },
      exact hx },
    { have hxn : x = ‚ü®n, nat.lt_succ_self _‚ü©, from fin.eq_of_veq (nat.eq_of_lt_succ_of_not_lt x.is_lt hx),
      rw hxn, apply vector.cons_inj_left h }
  end


end fix_q

noncomputable def crq (r : ‚Ñù) (q : ‚Ñï) := ((one_coeff_poly q).eval‚ÇÇ coe r) / r ^ ((q-1 : ‚Ñù)/3)

lemma one_coeff_poly_support (q : ‚Ñï) : finsupp.support (one_coeff_poly q) = finset.range q :=
begin
  ext x,
  constructor,
  { intro hx,
    have : (one_coeff_poly q).coeff x ‚â† 0, from finsupp.mem_support_iff.1 hx,
    rw finset.mem_range,
    apply lt_of_not_ge,
    exact mt (one_coeff_poly_coeff_ge _) this },
  { intro hx,
    rw finsupp.mem_support_iff,
    apply ne_of_gt,
    convert zero_lt_one,
    apply one_coeff_poly_coeff_lt,
    simpa using hx }
end

lemma one_coeff_poly_nat_degree {q : ‚Ñï} (hq : q > 0) : (one_coeff_poly q).nat_degree = q - 1 :=
begin
  have hq : ‚àÉ n, q = n + 1, from nat.exists_eq_succ_of_ne_zero (ne_of_gt hq),
  cases hq with n hn,
  rw [hn, polynomial.nat_degree, polynomial.degree, one_coeff_poly_support, finset.sup_range],
  simp [option.get_or_else]
end

lemma one_coeff_poly_pow_nat_degree {q : ‚Ñï} (n : ‚Ñï) (hq : q > 0) :
  ((one_coeff_poly q)^n).nat_degree = (q-1)*n :=
begin
  rw [polynomial.nat_degree_pow_eq', one_coeff_poly_nat_degree hq, mul_comm],
  apply ne_of_gt,
  apply pow_pos,
  rw [polynomial.leading_coeff, one_coeff_poly_nat_degree hq, one_coeff_poly_coeff_lt],
  { exact zero_lt_one },
  { apply nat.sub_lt hq zero_lt_one }
end

section
variables {Œ± : Type} [discrete_field Œ±] [fintype Œ±] (n : ‚Ñï)
local notation `m` := m Œ± n
local notation `q` := @q Œ± _ _

lemma q_ge_two : q ‚â• 2 :=
finset.card_le_of_inj_on (Œª n, if n = 0 then 0 else 1) (Œª _ _, finset.mem_univ _) $ Œª i j hi hj,
  begin
    split_ifs; simp *,
    have hi' : i = 1,
      from nat.eq_of_lt_succ_of_not_lt hi (not_lt_of_ge (nat.succ_le_of_lt (nat.pos_of_ne_zero h))),
    have hj' : j = 1,
      from nat.eq_of_lt_succ_of_not_lt hj (not_lt_of_ge (nat.succ_le_of_lt (nat.pos_of_ne_zero h_1))),
    rwa ‚Üêhj' at hi'
  end

theorem lemma_13_8 {d : ‚Ñö} (hd : d ‚â• 0) :
  m d = (finset.range (‚åäd‚åã.nat_abs + 1)).sum (cf q n) :=
begin
  have hbc : finset.card (finset.univ.filter (Œª v : fin n ‚Üí fin q, ‚Üë(fin.sum (‚Üëv : fin n ‚Üí ‚Ñï)) ‚â§ d)) = m d,
  { have h := h_B_card Œ± n d,
    simp only [l125_A, l125_B, total_degree_monom] at h,
    convert h },
  have hse : finset.card (finset.univ.filter (Œª v : fin n ‚Üí fin q, ‚Üë(fin.sum (‚Üëv : fin n ‚Üí ‚Ñï)) ‚â§ d)) = finset.card (finset.bind (finset.range (‚åäd‚åã.nat_abs + 1)) (Œª k, sf q n k)),
  { apply finset.card_congr (Œª v _, vector_of_fin_fn v),
    { intros _ ha, exact vector_of_fin_fn_mem_bind (finset.mem_filter.1 ha).2 },
    { intros _ _ _ _ hv1v2, exact vector_of_fin_fn_inj hv1v2 },
    { dsimp, intros b hb,
      exact ‚ü® fin_fn_of_vector b, finset.mem_filter.2 ‚ü®finset.mem_univ _, (inv_in_range hd hb)‚ü©,
              vector_of_fin_fn_inv _ ‚ü© }},
  rw [‚Üêhbc, hse, finset.card_bind], refl,
  intros _ _ _ _, apply sf_disjoint
end

lemma int.cast_nat_abs {z : ‚Ñ§} (hz : 0 ‚â§ z) : (z.nat_abs : ‚Ñù) = z :=
by norm_cast; exact int.nat_abs_of_nonneg hz

lemma one_coeff_poly_pow_eval‚ÇÇ_nonneg {r : ‚Ñù} (hr : 0 ‚â§ r) (k) : 0 ‚â§ polynomial.eval‚ÇÇ coe r (one_coeff_poly k ^ n) :=
begin
  rw polynomial.eval‚ÇÇ_pow,
  apply pow_nonneg,
  apply one_coeff_poly_eval‚ÇÇ_nonneg,
  { exact nat.cast_nonneg },
  { assumption }
end

lemma finset_sum_range {r : ‚Ñù} (hr : 0 < r) (hr2 : r < 1) :
  (finset.range ((q - 1) * n + 1)).sum (Œª (j : ‚Ñï), r ^ j * ‚Üë(cf q n j)) =
     ((one_coeff_poly q) ^ n).eval‚ÇÇ coe r :=
begin
  convert polynomial.poly_eq_deg_sum _ _ _,
  { rw one_coeff_poly_pow_nat_degree _ q_pos },
  { ext, rw [lemma_13_9, mul_comm], exact q_pos },
  { simp }
end

theorem theorem_14_1 {r : ‚Ñù} (hr : 0 < r) (hr2 : r < 1) : ‚Üë(m (((q : ‚Ñö)- 1)*n / 3)) ‚â§ (crq r q) ^ n :=
let e := ‚åä((q - 1 : ‚Ñù))*n / 3‚åã.nat_abs in
have hpos : ((q - 1 : ‚Ñù))*n / 3 ‚â• 0, from div_nonneg (mul_nonneg (sub_nonneg_of_le (by exact_mod_cast one_le_q)) (nat.cast_nonneg _)) (by norm_num),
have r^e * m (((q : ‚Ñö)- 1)*n / 3) = (finset.range (e+1)).sum (Œª j, r^e * cf q n j),
begin
  rw [lemma_13_8, ‚Üêfinset.sum_hom coe, finset.mul_sum],
  { congr' 2, dsimp [e], congr' 2, rw ‚Üê@rat.cast_floor ‚Ñù _ _ (((q : ‚Ñö) + -1) * n /3), simp },
  { apply_instance },
  { assumption_mod_cast }
end,
have hmr : ‚àÄ x, x ‚àà finset.range (e + 1) ‚Üí x < (q-1)*n + 1, from Œª x hx,
begin
  dsimp only [e] at hx,
  have hx' := finset.mem_range.1 hx,
  apply lt_of_lt_of_le hx',
  apply nat.succ_le_succ,
  suffices : (int.nat_abs ‚åä((q : ‚Ñù) -1)*n / 3‚åã : ‚Ñù) ‚â§ ‚Üë((q-1)*n), from nat.cast_le.1 this,
  rw int.cast_nat_abs,
  { apply le_trans,
    { apply floor_le },
    { apply (div_le_iff _).2,
      convert le_mul_of_ge_one_right _ _,
      { simp [one_le_q], },
      { apply mul_nonneg,
        { apply sub_nonneg_of_le, exact_mod_cast one_le_q },
        { apply nat.cast_nonneg } },
      repeat { norm_num } } },
  rw floor_nonneg,
  exact hpos
end,
have r^e * m (((q : ‚Ñö)- 1)*n / 3) ‚â§ ((one_coeff_poly q)^n).eval‚ÇÇ coe r, from calc
     _ = (finset.range (e+1)).sum (Œª j, r^e * cf q n j) : this
   ... ‚â§ (finset.range (e+1)).sum (Œª j, r^j * cf q n j) : finset.sum_le_sum $ Œª x hx,mul_le_mul_of_nonneg_right (pow_le_pow_of_le_one (le_of_lt hr) (le_of_lt hr2) (by linarith [finset.mem_range.1 hx])) (nat.cast_nonneg _)
   ... ‚â§  (finset.range ((q - 1) * n + 1)).sum (Œª j, r^j * cf q n j) : finset.sum_le_sum_of_subset_of_nonneg (Œª x hx, finset.mem_range.2 (hmr _ hx)) $
            Œª x hx1 hx2, mul_nonneg (pow_nonneg (le_of_lt hr) _) (nat.cast_nonneg _)
   ... = ((one_coeff_poly q)^n).eval‚ÇÇ coe r : finset_sum_range _ hr hr2,
calc _ ‚â§ ((one_coeff_poly q)^n).eval‚ÇÇ coe r / r^e : (le_div_iff (pow_pos hr _)).2 (by convert this using 1; apply mul_comm)
   ... ‚â§ ((one_coeff_poly q)^n).eval‚ÇÇ coe r / r^(((q - 1 : ‚Ñù))*n / 3) : div_le_div_of_le_left
         (one_coeff_poly_pow_eval‚ÇÇ_nonneg _ (le_of_lt hr) _)
         (pow_pos hr _)
         (real.rpow_pos_of_pos hr _)
         begin rw ‚Üêreal.rpow_nat_cast, apply real.rpow_le hr (le_of_lt hr2), dsimp [e], rw int.cast_nat_abs, apply floor_le, apply floor_nonneg.2 hpos end
   ... = ((one_coeff_poly q)^n).eval‚ÇÇ coe r / (r^(((q - 1 : ‚Ñù)) / 3))^n : begin rw [‚Üêreal.rpow_nat_cast _ n, ‚Üêreal.rpow_mul], {congr, ring}, {exact le_of_lt hr} end
   ... = ((one_coeff_poly q).eval‚ÇÇ coe r / (r^(((q - 1 : ‚Ñù)) / 3)))^n : begin rw [polynomial.eval‚ÇÇ_pow, ‚Üêdiv_pow], apply ne_of_gt, apply real.rpow_pos_of_pos hr end


end

lemma one_coeff_poly_eval_ge_one {r : ‚Ñù} {q : ‚Ñï} (hr : 0 ‚â§ r) (hq : 1 ‚â§ q) :
  1 ‚â§ (one_coeff_poly q).eval‚ÇÇ coe r :=
begin
  cases nat.exists_eq_succ_of_ne_zero (ne_of_gt (lt_of_lt_of_le zero_lt_one hq)) with k hqk,
  rw [hqk, polynomial.eval‚ÇÇ, finsupp.sum, one_coeff_poly_support, finset.sum_range_succ', pow_zero, mul_one],
  change 1 ‚â§ _ + (‚Üë((one_coeff_poly (k+1)).coeff 0) : ‚Ñù),
  rw [one_coeff_poly_coeff_lt (k+1) (nat.succ_pos _)],
  convert (le_add_iff_nonneg_left _).2 _,
  { simp },
  { apply finset.zero_le_sum,
    intros x hx,
    apply mul_nonneg,
    { convert zero_le_one,
      show ((one_coeff_poly (k+1)).coeff (x+1) : ‚Ñù) = 1,
      rw [one_coeff_poly_coeff_lt]; simp,
      simpa using hx },
    { apply pow_nonneg,
      exact hr } }
end

lemma crq_ge_one {r : ‚Ñù} {q : ‚Ñï} (hr : 0 < r) (hr2 : r ‚â§ 1) (hq : 1 ‚â§ q) : 1 ‚â§ crq r q :=
begin
  rw [crq, le_div_iff, one_mul],
  { transitivity (1 : ‚Ñù),
    { apply real.rpow_le_one _ (le_of_lt hr) hr2,
      apply div_nonneg,
      { apply sub_nonneg_of_le,
        suffices : ((1 : ‚Ñï) : ‚Ñù) ‚â§ q, by simpa using this,
        exact nat.cast_le.2 hq },
        { norm_num } },
    { apply one_coeff_poly_eval_ge_one (le_of_lt hr) hq } },
  { apply real.rpow_pos_of_pos hr }
end

lemma crq_eq (r : ‚Ñù) (q : ‚Ñï) :
  crq r q = (finset.range q).sum (Œªi:‚Ñï, r ^ i) / r ^ ((q - 1 : ‚Ñù) / 3) :=
begin
  rw [crq, one_coeff_poly, ‚Üê finset.sum_hom (polynomial.eval‚ÇÇ (coe : ‚Ñï ‚Üí ‚Ñù) r)],
  congr' 2, funext i,
  rw [polynomial.eval‚ÇÇ_pow, polynomial.eval‚ÇÇ_X]
end

theorem lemma_13_15 {q : ‚Ñï} (hq : 2 ‚â§ q) : ‚àÉ r : ‚Ñù, 0 < r ‚àß r < 1 ‚àß crq r q < q :=
begin
  have q_pos : 0 < q := le_trans dec_trivial hq,
  have q_eq : q = (q - 1) + 1 := (nat.sub_add_cancel $ le_trans dec_trivial hq).symm,
  have q_pred : ((q - 1 : ‚Ñï) : ‚Ñù) = (q : ‚Ñù) - 1,
  { rw [q_eq] { occs := occurrences.pos [2] },
    rw [nat.cast_add, nat.cast_one, add_sub_cancel] },
  have sum_pow_one : (finset.range q).sum (pow 1 : ‚Ñï ‚Üí ‚Ñù) = (finset.range q).sum (Œªi, (1 : ‚Ñù)),
  { congr, funext i, rw one_pow },
  let f : ‚Ñù ‚Üí ‚Ñù := Œªr, ((finset.range q).sum (Œªi, r ^ i)) ^ 3 - (q : ‚Ñù)^3 * r^(q-1),
  let f' : ‚Ñù ‚Üí ‚Ñù := Œªr,
    3 * (finset.range q).sum (Œªi, r ^ i) ^ 2 * (finset.range (q - 1)).sum (Œªi, (i + 1) * r ^ i) -
    (q : ‚Ñù)^3 * (q - 1) * r^(q-2),
  have f1 : f 1 = 0,
  { simp only [f],
    rw [sum_pow_one, finset.sum_const, finset.card_range, add_monoid.smul_one, one_pow,
      mul_one, sub_self] },
  have f'1 : 0 < f' 1,
  { simp only [f'],
    rw [one_pow, sum_pow_one, finset.sum_const, finset.card_range, add_monoid.smul_one, mul_one],
    conv { find (finset.sum _ _) { congr, skip, funext, rw [one_pow, mul_one] } },
    have : ((2:‚Ñï) : ‚Ñù) ‚â† 0, { rw [(‚â†), nat.cast_eq_zero], exact dec_trivial },
    rw [finset.sum_add_distrib, finset.sum_const, finset.card_range, add_monoid.smul_one,
      add_comm, ‚Üê finset.sum_range_succ, ‚Üê finset.sum_nat_cast, ‚Üê nat.add_one, ‚Üê q_eq,
      ‚Üê mul_div_cancel (coe (finset.sum _ _ : ‚Ñï) : ‚Ñù) this, ‚Üê nat.cast_mul,
      finset.sum_range_id_mul_two, nat.cast_mul, mul_assoc, ‚Üê mul_div_assoc, mul_div_comm,
      sub_pos, ‚Üê mul_assoc, ‚Üê mul_one (_ * _ : ‚Ñù), ‚Üê pow_succ', q_pred],
    refine mul_lt_mul_of_pos_left _ _,
    { suffices : ((1 : ‚Ñï) : ‚Ñù) < (3 : ‚Ñï) / (2 : ‚Ñï),
      { simpa },
      rw [lt_div_iff, ‚Üê nat.cast_mul, nat.cast_lt],
      exact dec_trivial,
      rw [‚Üê nat.cast_zero, nat.cast_lt],
      exact dec_trivial },
    { refine mul_pos (pow_pos (nat.cast_pos.2 q_pos) _) (sub_pos.2 _),
      rw [‚Üê nat.cast_one, nat.cast_lt],
      exact hq } },
  have has_deriv_f_f' : ‚àÄr, has_deriv f (f' r) r,
  { assume r,
    refine has_deriv.sub ((has_deriv.pow (has_deriv_finset_sum _ _) 3).congr‚ÇÇ _) _,
    { exact Œªi:‚Ñï, i * r ^ (i - 1) },
    { congr' 1,
      { simp only
          [nat.succ_sub_succ_eq_sub, eq_self_iff_true, nat.cast_bit1, nat.sub_zero, nat.cast_one] },
      { rw [q_eq] { occs := occurrences.pos [1] },
        rw [finset.sum_range_succ'],
        simp only [add_zero, nat.succ_sub_succ_eq_sub, nat.zero_sub, nat.cast_zero, zero_mul,
          add_comm, eq_self_iff_true, (‚àò), nat.cast_succ, nat.sub_zero, pow_zero, zero_add] } },
    { refine assume n, (has_deriv.pow (has_deriv_id r) n).congr‚ÇÇ _,
      rw mul_one },
    { rw mul_assoc,
      refine ((has_deriv.pow (has_deriv_id r) _).congr‚ÇÇ _).mul_left _,
      rw [q_eq] { occs := occurrences.pos [3] },
      rw [nat.cast_add, nat.cast_one, add_sub_cancel, mul_one],
      refl } },
  rcases decreasing_of_fderiv_pos _ _ _ (has_deriv_f_f' 1) f'1 with ‚ü®Œµ, hŒµ, h‚ü©,
  let Œµ' : ‚Ñù := 1 - (min Œµ 1) / 2,
  have hŒµ‚ÇÅ : 1 - Œµ < Œµ',
  { refine (sub_lt_sub_iff_left _).2 (lt_of_le_of_lt (div_le_div_of_le_of_pos (min_le_left _ _) _) _);
      linarith },
  have hŒµ‚ÇÇ : Œµ' < 1,
  { refine (sub_lt_self _ $ div_pos (lt_min hŒµ _) _);
      norm_num },
  have hŒµ‚ÇÉ : 0 < Œµ',
  { refine lt_sub.2 (lt_of_le_of_lt (div_le_div_of_le_of_pos (min_le_right _ _) _) _); norm_num },
  refine ‚ü®Œµ', hŒµ‚ÇÉ, hŒµ‚ÇÇ, _‚ü©,
  specialize h Œµ' hŒµ‚ÇÅ hŒµ‚ÇÇ,
  rw f1 at h,
  simp only [f, sub_lt_iff_lt_add, zero_add] at h,
  rw [crq_eq, div_lt_iff],
  refine lt_of_pow_lt_pow 3 _ _,
  { exact zero_le_mul (nat.cast_nonneg _) (real.rpow_nonneg_of_nonneg (le_of_lt hŒµ‚ÇÉ) _) },
  { have : ((3 : ‚Ñï) : ‚Ñù) = 3 := by norm_num,
    rwa [mul_pow, ‚Üê real.rpow_nat_cast (Œµ' ^ _), ‚Üê real.rpow_mul (le_of_lt hŒµ‚ÇÉ), this,
      div_mul_cancel, ‚Üê q_pred, real.rpow_nat_cast],
    norm_num },
  { exact real.rpow_pos_of_pos hŒµ‚ÇÉ _ }
end

lemma coeff_ge_one {r : ‚Ñù} (hr : 0 < r) (hr2 : r < 1) {q : ‚Ñï} (hq : q ‚â• 1) :
  ((3 * (crq r q)^2)/(1 - r)) ‚â• 1 :=
have 1 ‚â§ crq r q^2, from one_le_pow_of_one_le (crq_ge_one hr (le_of_lt hr2) hq) _,
by apply (le_div_iff _).2; linarith

lemma finset.card_le_card_univ {Œ±} [fintype Œ±] [decidable_eq Œ±] (A : finset Œ±) : A.card ‚â§ (@finset.univ Œ± _).card :=
finset.card_le_card_of_inj_on id (Œª _ _, finset.mem_univ _) $ by simp


section
set_option class.instance_max_depth 200
variables {Œ± : Type} [discrete_field Œ±] [fintype Œ±] {n : ‚Ñï} {a b c : Œ±} (hc : c ‚â† 0)
    (habc : a + b + c = 0) {A : finset (fin n ‚Üí Œ±)}
    (hall : ‚àÄ x y z : fin n ‚Üí Œ±, x ‚àà A ‚Üí y ‚àà A ‚Üí z ‚àà A ‚Üí a ‚Ä¢ x + b ‚Ä¢ y + c ‚Ä¢ z = 0 ‚Üí x = y ‚àß x = z)
include hc habc hall
local notation `q` := @q Œ± _ _

theorem theorem_13_14 {r : ‚Ñù} (hr : 0 < r) (hr2 : r < 1) : ‚ÜëA.card ‚â§ 3 * (crq r q)^n :=
if hn : n = 0 then
   have hcu : (@finset.univ (fin n ‚Üí Œ±) _).card = 1,
    by { convert finset.card_univ, rw hn, refl },
  have hac : A.card ‚â§ 1, by { convert finset.card_le_card_univ _, {exact eq.symm hcu}, {apply_instance}},
  have hac : (A.card : ‚Ñù) ‚â§ 1, from suffices (A.card : ‚Ñù) ‚â§ (1 : ‚Ñï), by simpa, nat.cast_le.2 hac,
  le_trans hac (by rw [hn, pow_zero, mul_one]; norm_num)
else have hn : n > 0, from nat.pos_of_ne_zero hn,
begin
  transitivity,
  { apply nat.cast_le.2,
    apply theorem_12_1 n hc habc hn hall },
  { rw nat.cast_mul,
    convert mul_le_mul_of_nonneg_left _ _,
    { norm_cast },
    { rw [mul_comm, ‚Üêdiv_eq_mul_one_div],
      apply theorem_14_1; assumption },
    { norm_num } }
end

end

section
set_option class.instance_max_depth 200

theorem general_cap_set {Œ± : Type} [discrete_field Œ±] [fintype Œ±] :
  ‚àÉ C B : ‚Ñù, B > 0 ‚àß C > 0 ‚àß
  C < fintype.card Œ± ‚àß ‚àÄ {a b c : Œ±} {n : ‚Ñï} {A : finset (fin n ‚Üí Œ±)},
   c ‚â† 0 ‚Üí a + b + c = 0 ‚Üí
   (‚àÄ x y z : fin n ‚Üí Œ±, x ‚àà A ‚Üí y ‚àà A ‚Üí z ‚àà A ‚Üí a ‚Ä¢ x + b ‚Ä¢ y + c ‚Ä¢ z = 0 ‚Üí x = y ‚àß x = z) ‚Üí
    ‚ÜëA.card ‚â§ B * C^n :=
begin
  rcases lemma_13_15 q_ge_two with ‚ü®B, hB, hB2, hBr‚ü©,
  repeat {apply exists.intro},
  refine ‚ü®_, _, hBr, Œª a b c n A hc habc hall, theorem_13_14 hc habc hall hB hB2‚ü©,
  { norm_num },
  { apply lt_of_lt_of_le zero_lt_one,
    exact crq_ge_one hB (le_of_lt hB2) one_le_q }
end
end

lemma one_coeff_poly_real_eval_aux (x : ‚Ñù) :
  (one_coeff_poly 3).eval‚ÇÇ coe x = (((1 : ‚Ñï) : ‚Ñù) * (1 : ‚Ñù))  + ( (((1 : ‚Ñï) : ‚Ñù) * (x * (1 : ‚Ñù))) + ((((1 : ‚Ñï) : ‚Ñù) * (x * (x * (1 : ‚Ñù)))) + (0 : ‚Ñù) )  ) :=
rfl

lemma one_coeff_poly_real_eval (x : ‚Ñù) : (one_coeff_poly 3).eval‚ÇÇ coe x = 1 + x + x^2 :=
by convert one_coeff_poly_real_eval_aux x using 1; simp [pow_two]

lemma calc_lemma (r a : ‚Ñù) (hr : r = (a - 1)/8) :
  (3 / 8) ^ 3 * (207 + 33 * (8 * r + 1)) * r ^ 2 - (1 + r + r ^ 2) ^ 3 =
  -(140481/262144) - (70389*a)/131072 + (14553*a^2)/262144 + (1215*a^3)/65536
   - (279*a^4)/262144 - (9*a^5)/131072 - a^6/262144 :=
by rw hr; ring

noncomputable def r : ‚Ñù := (real.sqrt 33 - 1) / 8

lemma r_pos : r > 0 :=
div_pos (sub_pos_of_lt (lt_of_pow_lt_pow _ (real.sqrt_nonneg _)
  (show (1 : ‚Ñù) ^ 2 < (real.sqrt 33)^2, by rw [real.sqr_sqrt, one_pow]; norm_num))) (by norm_num)

lemma r_lt_one : r < 1 :=
begin
  apply (div_lt_iff _).2,
  apply sub_lt_iff_lt_add.2,
  apply @lt_of_pow_lt_pow _ _ _ _ 2 _,
  rw real.sqr_sqrt,
  all_goals {norm_num}
end

lemma r_def : real.sqrt 33 = 8*r + 1 :=
by rw [r, mul_div_cancel', sub_add_cancel]; norm_num

lemma c_r_cubed : (crq r 3)^3 = ((3 : ‚Ñù) / 8)^3 * (207 + 33*real.sqrt 33) :=
begin
  rw [crq, one_coeff_poly_real_eval, div_pow],
  conv {to_lhs, congr, skip, rw [‚Üêreal.rpow_nat_cast _ 3, ‚Üêreal.rpow_mul (le_of_lt r_pos)], simp, norm_num},
  symmetry, apply eq_div_of_mul_eq,
  { apply ne_of_gt,
    apply real.rpow_pos_of_pos,
    exact r_pos },
  { rw [r_def],
    apply eq_of_sub_eq_zero,
    have hr2 : r^(2 : ‚Ñù) = r^((2 : ‚Ñï) : ‚Ñù), by simp,
    rw [hr2, real.rpow_nat_cast, calc_lemma r _ rfl],
    change 6 with 2+2+2, change 5 with 2+2+1, change 4 with 2 + 2, change 3 with 2+1,
    simp only [pow_add, pow_one, real.sqr_sqrt (show (0:‚Ñù) ‚â§ 33, by norm_num)],
    ring },
  { apply ne_of_gt,
    apply real.rpow_pos_of_pos,
    exact r_pos }
end

lemma c_r_cuberoot : crq r 3 = (((3 : ‚Ñù) / 8)^3 * (207 + 33*real.sqrt 33))^(1/3 : ‚Ñù) :=
have h : _ := congr_arg (Œª r : ‚Ñù, r^(1/3:‚Ñù)) c_r_cubed,
begin
  rw [‚Üêreal.rpow_nat_cast _ 3, ‚Üêreal.rpow_mul] at h,
  { simp at h,
    rw [mul_inv_cancel] at h,
    { simpa using h },
    { norm_num } },
  { linarith [crq_ge_one r_pos (le_of_lt r_lt_one) (show 1 ‚â§ 3, by norm_num)] }
end

section
set_option class.instance_max_depth 200

theorem theorem_13_17 {Œ± : Type} [discrete_field Œ±] [fintype Œ±] {a b c : Œ±}
  (hq : fintype.card Œ± = 3) (hc : c ‚â† 0) (habc : a + b + c = 0) :
  ‚àÉ B : ‚Ñù, ‚àÄ {n : ‚Ñï} {A : finset (fin n ‚Üí Œ±)},
  (‚àÄ x y z : fin n ‚Üí Œ±, x ‚àà A ‚Üí y ‚àà A ‚Üí z ‚àà A ‚Üí a ‚Ä¢ x + b ‚Ä¢ y + c ‚Ä¢ z = 0 ‚Üí x = y ‚àß x = z) ‚Üí
  ‚ÜëA.card ‚â§ B * ((((3 : ‚Ñù) / 8)^3 * (207 + 33*real.sqrt 33))^(1/3 : ‚Ñù))^n :=
begin
  apply exists.intro,
  intros n A ha,
  convert theorem_13_14 hc habc ha r_pos r_lt_one,
  rw [q, hq, c_r_cuberoot]
end

end

section
private lemma three_prime : nat.prime 3 := by norm_num
local notation `‚Ñ§/3‚Ñ§` := zmodp 3 three_prime

set_option class.instance_max_depth 100

theorem cap_set_problem : ‚àÉ B : ‚Ñù, ‚àÄ {n : ‚Ñï} {A : finset (fin n ‚Üí ‚Ñ§/3‚Ñ§)},
  (‚àÄ x y z : fin n ‚Üí ‚Ñ§/3‚Ñ§, x ‚àà A ‚Üí y ‚àà A ‚Üí z ‚àà A ‚Üí x + y + z = 0 ‚Üí x = y ‚àß x = z) ‚Üí
  ‚ÜëA.card ‚â§ B * ((((3 : ‚Ñù) / 8)^3 * (207 + 33*real.sqrt 33))^(1/3 : ‚Ñù))^n :=
begin
  have habc : (1 : ‚Ñ§/3‚Ñ§) + 1 + 1 = 0, from rfl,
  have hcard : fintype.card ‚Ñ§/3‚Ñ§ = 3, from zmodp.card_zmodp three_prime,
  have hone : (1 : ‚Ñ§/3‚Ñ§) ‚â† 0, from dec_trivial,
  cases theorem_13_17 hcard hone habc with B hB,
  use B,
  intros n A hxyz,
  apply hB,
  simpa using hxyz
end

theorem cap_set_problem_specific (n : ‚Ñï) {A : finset (fin n ‚Üí ‚Ñ§/3‚Ñ§)}
  (hxyz : ‚àÄ x y z : fin n ‚Üí ‚Ñ§/3‚Ñ§, x ‚àà A ‚Üí y ‚àà A ‚Üí z ‚àà A ‚Üí x + y + z = 0 ‚Üí x = y ‚àß x = z) :
  ‚ÜëA.card ‚â§ 3 * ((((3 : ‚Ñù) / 8)^3 * (207 + 33*real.sqrt 33))^(1/3 : ‚Ñù))^n :=
have hone : (1 : ‚Ñ§/3‚Ñ§) ‚â† 0, from dec_trivial,
have hq : @q (‚Ñ§/3‚Ñ§) _ _ = 3, from zmodp.card_zmodp three_prime,
begin
  transitivity,
  { convert theorem_13_14 hone _ _ r_pos r_lt_one,
    repeat {exact 1},
    { refl },
    { simpa using hxyz } },
  rw hq,
  convert mul_le_mul_of_nonneg_right _ _,
  { symmetry, exact c_r_cuberoot },
  { refl },
  { apply pow_nonneg,
    apply le_trans zero_le_one,
    apply crq_ge_one r_pos (le_of_lt r_lt_one),
    norm_num }
end

end