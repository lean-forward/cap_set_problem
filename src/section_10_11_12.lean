/-
Copyright (c) 2018 Sander Dahmen, Johannes H√∂lzl, Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sander Dahmen, Johannes H√∂lzl, Robert Y. Lewis

"On large subsets of ùîΩ‚Åø_q with no three-term arithmetic progression"
by J. S. Ellenberg and D. Gijswijt

This file establishes the bound on the cardinality of a generalized cap set.
It corresponds to sections 10-12 of our blueprint.
-/

import library section_9

open mv_polynomial

section

parameters
  {Œ± : Type} [discrete_field Œ±] [fintype Œ±] -- ùîΩ_q: q is cardinality of `Œ±`
  (n : ‚Ñï)

def q : ‚Ñï := fintype.card Œ±

lemma q_pos : q > 0 := fintype.card_pos_iff.2 ‚ü®1‚ü©

lemma one_le_q : 1 ‚â§ q := q_pos

lemma one_le_q_real : 1 ‚â§ (q : ‚Ñö) :=
suffices ‚Üë(1 : ‚Ñï) ‚â§ (q : ‚Ñö), by simpa,
nat.cast_le.2 one_le_q


/-- Polynomials in Œ± - seen as a vector space -/
@[reducible] def S := mv_polynomial (fin n) Œ±

instance S.module : module Œ± S := by dunfold S; apply_instance
instance S.vector_space : vector_space Œ± S := { .. S.module }

instance fin_q_has_zero : has_zero (fin q) := ‚ü®‚ü®0, one_le_q‚ü©‚ü©

def M : finset (mv_polynomial (fin n) Œ±):=
(finset.univ.image $ Œª f : fin n ‚Üí‚ÇÄ fin q, f.map_range fin.val rfl).image (Œªd : fin n ‚Üí‚ÇÄ ‚Ñï, monomial d (1:Œ±))

lemma linear_independent_M : linear_independent Œ± (‚ÜëM : set (mv_polynomial (fin n) Œ±)) :=
(mv_polynomial.is_basis_monomials _ _).1.mono $ Œª p hp,
  let ‚ü®s, _, hsp‚ü© := finset.mem_image.1 hp in
  set.mem_range.2 ‚ü®s, hsp‚ü©

def fin_map_to_mono_map (f : fin n ‚Üí fin q) : fin n ‚Üí‚ÇÄ ‚Ñï := Œª k, (f k).val
def fmtmm : has_coe (fin n ‚Üí fin q) (fin n ‚Üí‚ÇÄ ‚Ñï) := ‚ü®fin_map_to_mono_map‚ü©
local attribute [instance] fmtmm

lemma monomial_mem_M (f : fin n ‚Üí fin q) : monomial ‚Üëf (1 : Œ±) ‚àà M :=
begin
  apply finset.mem_image.2,
  refine ‚ü®f, _, rfl‚ü©,
  apply finset.mem_image.2,
  use [f, finset.mem_univ _], ext, refl
end

@[simp] lemma coe_fin_eq_coe_fin (f g : (fin n ‚Üí fin q)) : (f : fin n ‚Üí‚ÇÄ ‚Ñï) = g ‚Üî f = g :=
begin
  split,
  { intro h,
    ext,
    apply (fin.ext_iff _ _).2,
    apply finsupp.congr_fun h },
  { intro h, rw h }
end

lemma M_spec {x} (hx : x ‚àà M) : ‚àÉ s : fin n ‚Üí fin q, monomial ‚Üës 1 = x :=
begin
  rcases finset.mem_image.1 hx with ‚ü®d, ‚ü®hd, hdmon‚ü©‚ü©,
  have h : ‚àÄ k, d k < q,
  { intro k,
    rcases finset.mem_image.1 hd with ‚ü®l, ‚ü®hl, hdl‚ü©‚ü©,
    have hdl' : (finsupp.map_range fin.val (_) l : fin n ‚Üí ‚Ñï) k = d k, {rw hdl},
    rw finsupp.map_range_apply at hdl', rw ‚Üêhdl', apply fin.is_lt },
  use Œª k, ‚ü®d k, h k‚ü©,
  rw ‚Üêhdmon, congr, ext, refl
end

noncomputable def monom_exps (p : (mv_polynomial (fin n) Œ±)) : (fin n ‚Üí fin q) :=
if hp : p ‚àà M then classical.some (M_spec hp) else 0

lemma monom_exps_def {x} (hx : x ‚àà M) : monomial ‚Üë(monom_exps x) 1 = x :=
by simpa [hx, monom_exps] using (classical.some_spec (M_spec hx))

lemma monom_exps_inj {x y} (hx : x ‚àà M) (hy : y ‚àà M) (hxy : monom_exps x = monom_exps y) : x = y :=
have h1 : _, from monom_exps_def hx, have h2 : _, from monom_exps_def hy,
by rw [‚Üêh1, ‚Üêh2, hxy]

@[simp] lemma monom_exps_monomial (f : fin n ‚Üí fin q) : monom_exps (monomial f 1) = f :=
by simpa using finsupp.eq_of_single_eq_left (monom_exps_def (monomial_mem_M _)) one_ne_zero

lemma monom_exps_surj (f : fin n ‚Üí fin q) : ‚àÉ x, x ‚àà M ‚àß monom_exps x = f :=
‚ü®monomial f 1, monomial_mem_M _, monom_exps_monomial _‚ü©

-- do more generally
lemma monomial_total_degree (f : fin n ‚Üí‚ÇÄ ‚Ñï) :
  mv_polynomial.total_degree (monomial f (1 : Œ±)) = finsupp.sum f (Œª _, id) :=
suffices finset.sup {f} (Œª (s : fin n ‚Üí‚ÇÄ ‚Ñï), finsupp.sum s (Œª _, id)) =
    finsupp.sum f (Œª _, id), by simpa [monomial, finsupp.single, mv_polynomial.total_degree],
by rw finset.sup_singleton

lemma mv_polynomial_eval_eq (p : mv_polynomial (fin n) Œ±) (a : fin n ‚Üí Œ±) :
  p.eval a = p.sum (Œªi c, c * (i.to_multiset.map $ Œªn, a n).prod) :=
begin
  rw [eval, eval‚ÇÇ],
  congr, funext i c, congr,
  rw [finsupp.to_multiset_map, finsupp.prod_to_multiset,
    finsupp.prod_map_domain_index pow_zero pow_add]
end

lemma eval_add (p : S) (a b : fin n ‚Üí Œ±) :
  p.eval (a + b) = p.sum (Œªe x, multiset.sum $
    e.to_multiset.diagonal.map $ Œªp, x * ((p.1.map $ Œªi, a i).prod * (p.2.map $ Œªi, b i).prod)) :=
by simp [mv_polynomial_eval_eq, multiset.sum_map_mul_left, multiset.prod_map_add]

section

def M' (d : ‚Ñö) := M.filter (Œª m, d ‚â• mv_polynomial.total_degree m)
end

lemma linear_independent_M' (d) : linear_independent Œ± (‚Üë(M' d) : set (mv_polynomial (fin n) Œ±)) :=
linear_independent_M.mono $ Œª x hx, M.filter_subset hx

lemma mem_M_of_mem_M' {x d} (hx : x ‚àà M' d) : x ‚àà M := (finset.mem_filter.1 hx).1

def S' (d : ‚Ñö) := submodule.span Œ± (‚Üë(M' d) : set (mv_polynomial (fin n) Œ±))

lemma mem_S'_of_mem_M' {d : ‚Ñö} {p} (hp : p ‚àà M' d) : p ‚àà S' d :=
submodule.mem_span.2 $ Œª t hmt, hmt hp

lemma S'_le_restrict_q (d : ‚Ñö) : S' d ‚â§ mv_polynomial.restrict_degree _ _ (q - 1) :=
submodule.span_le.2 $ assume p hp,
begin
  have : p ‚àà M := mem_M_of_mem_M' hp,
  rcases M_spec this with ‚ü®s, rfl‚ü©,
  assume s hs n,
  rw [finset.mem_coe, mv_polynomial.monomial,
    finsupp.support_single_ne_zero zero_ne_one.symm, finset.singleton_eq_singleton,
    finset.mem_singleton] at hs,
  subst hs,
  exact (nat.le_sub_right_iff_add_le one_le_q).2 (s n).2
end

lemma le_floor_to_nat (n : ‚Ñï) (d : ‚Ñö) (hd : 0 ‚â§ d) :
  n ‚â§ ‚åäd‚åã.to_nat ‚Üî (n : ‚Ñö) ‚â§ d :=
begin
  rw [int.to_nat_le_iff, le_floor, ‚Üê coe_coe],
  rwa [le_floor]
end

lemma S'_le_restrict_d (d : ‚Ñö) (hd : 0 ‚â§ d):
  S' d ‚â§ mv_polynomial.restrict_total_degree _ _ (‚åäd‚åã).to_nat :=
submodule.span_le.2 $ assume p hp, (mv_polynomial.mem_restrict_total_degree _ _ _ _).2
begin
  have hp := (finset.mem_filter.1 hp).2,
  rwa [le_floor_to_nat _ _ hd]
end

parameter (Œ±)
noncomputable def m (d : ‚Ñö) : ‚Ñï := (vector_space.dim Œ± (S' d)).to_nat
parameter {Œ±}

lemma M'_card (d : ‚Ñö) : (M' d).card = m d :=
by rw [m, S', dim_span (linear_independent_M' _), ‚Üêcardinal.finset_card, cardinal.to_nat_coe]

section Propostion_11_1
parameters {d : ‚Ñö} (d_nonneg : 0 ‚â§ d) --(d_upper : d ‚â§ (fintype.card Œ±) * n)

set_option class.instance_max_depth 200

parameters
  {p : S}
  (A : finset (fin n ‚Üí Œ±))
  (a b c : Œ±)
  (hp : p ‚àà S' d)
  (habc : a + b + c = 0)
  (h : ‚àÄx:fin n ‚Üí Œ±, x‚ààA ‚Üí ‚àÄy‚ààA, x ‚â† y ‚Üí mv_polynomial.eval (a ‚Ä¢ x + b ‚Ä¢ y) p = (0:Œ±))

include habc
lemma a_plus_b : a + b = -c :=
calc a + b = a + b + c + (-c) : by simp
       ... = -c : by simp [habc]
omit habc

include hp

section
open multiset finsupp


/-- The coefficients `(m‚ÇÅ, m‚ÇÇ) ‚ä¢> ‚àë c*X^e ‚àà p | m‚ÇÅ + m‚ÇÇ = e, c * a ^ |m‚ÇÅ| * b ^ |m‚ÇÇ|` -/
def coeff (a b : Œ±) : (multiset (fin n) √ó multiset (fin n)) ‚Üí‚ÇÄ Œ± :=
p.sum $ Œªe c, sum $ e.to_multiset.diagonal.map $ Œªm, single m (c * a ^ card m.1 * b ^ card m.2)

/-- `œÄ x m := Œ†i‚ààm, x i`  -/
def œÄ (x : fin n ‚Üí Œ±) (m : multiset (fin n)) : Œ± := prod $ m.map $ Œªi, x i

def split_left (a b : Œ±) : (multiset (fin n) √ó multiset (fin n)) ‚Üí‚ÇÄ Œ± :=
(coeff a b).filter $ Œªp, d / 2 > p.1.card

def split_right (a b : Œ±) : (multiset (fin n) √ó multiset (fin n)) ‚Üí‚ÇÄ Œ± :=
((coeff a b).filter (Œªp:multiset (fin n)√ómultiset (fin n), ¬¨ d / 2 > p.1.card)).map_domain prod.swap

lemma mem_coeff_support {a b : Œ±} (m‚ÇÄ m‚ÇÅ : multiset (fin n)) (h : (m‚ÇÄ, m‚ÇÅ) ‚àà (coeff a b).support) :
  of_multiset (m‚ÇÄ + m‚ÇÅ) ‚àà p.support :=
begin
  rcases finsupp.mem_support_finset_sum _ h with ‚ü®f, hf, hfm‚ü©,
  rcases finsupp.mem_support_multiset_sum _ hfm with ‚ü®e, he, hem‚ü©, clear hfm,
  rcases multiset.mem_map.1 he with ‚ü®m, hm, rfl‚ü©, clear he,
  rw [mem_support_single] at hem,
  rcases hem with ‚ü®rfl, hb‚ü©,
  rw [mem_diagonal] at hm,
  rw [hm],
  show equiv_multiset.inv_fun (equiv_multiset.to_fun f) ‚àà p.support,
  rwa [equiv_multiset.left_inv f]
end

section
include d_nonneg

lemma mem_coeff_support_d {a b : Œ±} (m‚ÇÄ m‚ÇÅ : multiset (fin n))
  (h : (m‚ÇÄ, m‚ÇÅ) ‚àà (coeff a b).support) :
  d ‚â• (m‚ÇÄ + m‚ÇÅ).card :=
begin
  have : of_multiset (m‚ÇÄ + m‚ÇÅ) ‚àà p.support := mem_coeff_support m‚ÇÄ m‚ÇÅ h,
  have hp := (mv_polynomial.mem_restrict_total_degree _ _ _ _).1 (S'_le_restrict_d d d_nonneg hp),
  rw [le_floor_to_nat _ _ d_nonneg, total_degree_eq] at hp,
  refine le_trans (nat.cast_le.2 $ le_trans _ (finset.le_sup this)) hp,
  convert le_refl _,
  exact finsupp.equiv_multiset.4 _
end

lemma mem_coeff_support_q {a b : Œ±} (m‚ÇÄ m‚ÇÅ : multiset (fin n))
  (h : (m‚ÇÄ, m‚ÇÅ) ‚àà (coeff a b).support) (i : fin n) :
  (m‚ÇÄ + m‚ÇÅ).count i < q :=
begin
  have := S'_le_restrict_q _ hp (mem_coeff_support _ _ h),
  exact (nat.le_sub_right_iff_add_le one_le_q).1 (this i)
end

lemma split_left_curry_card (a b : Œ±) : (split_left a b).curry.support.card ‚â§ m (d/2) :=
begin
  rw [‚Üê M'_card, M'],
  refine finset.card_le_card_of_inj_on (Œªm, monomial (finsupp.of_multiset m) (1:Œ±)) _ _,
  { simp only [finset.mem_filter, finset.mem_image, exists_prop, finset.mem_univ, true_and, M],
    assume m hm',
    rcases finset.mem_image.1 (finsupp.support_curry _ hm') with ‚ü®‚ü®m‚ÇÄ, m‚ÇÅ‚ü©, hm, rfl‚ü©, clear hm',
    rw [split_left, finsupp.support_filter, finset.mem_filter] at hm,
    let f : fin n ‚Üí fin q := Œªi, ‚ü®m‚ÇÄ.count i, lt_of_le_of_lt
      (multiset.count_le_of_le _ $ le_add_right _ _) (mem_coeff_support_q _ _ hm.1 i)‚ü©,
    refine ‚ü®‚ü®_, ‚ü®f, rfl‚ü©, _‚ü©, _‚ü©,
    { congr, ext i, refl },
    { rw [monomial_total_degree, ‚Üê card_to_multiset],
      convert le_of_lt hm.2,
      exact finsupp.equiv_multiset.4 _ } },
  { assume m‚ÇÄ _ m‚ÇÅ _ eq,
    have := (single_eq_single_iff _ _ _ _).1 eq,
    simp only [(zero_ne_one : (0:Œ±) ‚â† 1).symm, or_false, and_false] at this,
    exact (finsupp.equiv_multiset.symm.apply_eq_iff_eq m‚ÇÄ m‚ÇÅ).1 this.1 }
end

lemma split_right_curry_card (a b : Œ±) : (split_right a b).curry.support.card ‚â§ m (d/2) :=
begin
  rw [‚Üê M'_card, M'],
  refine finset.card_le_card_of_inj_on (Œªm, monomial (finsupp.of_multiset m) (1:Œ±)) _ _,
  { simp only [finset.mem_filter, finset.mem_image, exists_prop, finset.mem_univ, true_and, M],
    assume m hm',
    rcases finset.mem_image.1 (finsupp.support_curry _ hm') with ‚ü®‚ü®m‚ÇÅ, m‚ÇÄ‚ü©, hm'', rfl‚ü©, clear hm',
    rcases finset.mem_image.1 (map_domain_support hm'') with ‚ü®‚ü®m‚ÇÄ', m‚ÇÅ'‚ü©, hm, eq‚ü©,
    cases eq, clear eq, clear hm'',
    rw [finsupp.support_filter, finset.mem_filter] at hm,
    let f : fin n ‚Üí fin q := Œªi, ‚ü®m‚ÇÅ.count i, lt_of_le_of_lt
      (multiset.count_le_of_le _ $ le_add_left _ _) (mem_coeff_support_q _ _ hm.1 i)‚ü©,
    refine ‚ü®‚ü®_, ‚ü®f, rfl‚ü©, _‚ü©, _‚ü©,
    { congr, ext i, refl },
    { rw [monomial_total_degree, ‚Üê card_to_multiset],
      have d_le : d / 2 ‚â§ ‚Üë(card m‚ÇÄ) := le_of_not_gt hm.2,
      have d_ge : d ‚â• card (m‚ÇÄ + m‚ÇÅ) := mem_coeff_support_d _ _ hm.1,
      rw [multiset.card_add, nat.cast_add] at d_ge,
      have : d / 2 ‚â• ‚Üë(card m‚ÇÅ),
      { refine le_of_not_gt (assume h, _),
        have : (card m‚ÇÄ : ‚Ñö) + card m‚ÇÅ > d / 2 + d / 2 := add_lt_add_of_le_of_lt d_le h,
        rw [add_halves] at this,
        exact not_lt_of_ge d_ge this },
      convert this,
      exact finsupp.equiv_multiset.4 _ } },
  { assume m‚ÇÄ _ m‚ÇÅ _ eq,
    have := (single_eq_single_iff _ _ _ _).1 eq,
    simp only [(zero_ne_one : (0:Œ±) ‚â† 1).symm, or_false, and_false] at this,
    exact (finsupp.equiv_multiset.symm.apply_eq_iff_eq m‚ÇÄ m‚ÇÅ).1 this.1 }
end

end

lemma eval_add_eq_split_sum {x y : fin n ‚Üí Œ±} {a b : Œ±} :
  p.eval (a ‚Ä¢ x + b ‚Ä¢ y) =
    (split_left a b).curry.sum (Œªm f, œÄ x m * f.sum (Œªm c, c * œÄ y m)) +
    (split_right a b).curry.sum (Œªm f, œÄ y m * f.sum (Œªm c, c * œÄ x m)) :=
calc p.eval (a ‚Ä¢ x + b ‚Ä¢ y) = (coeff a b).sum (Œªm c, c * œÄ x m.1 * œÄ y m.2) :
  by
    rw [coeff, eval_add];
    simp [œÄ, finsupp.multiset_sum_sum_index, sum_sum_index, sum_single_index,
      add_mul, mul_comm, mul_left_comm, mul_assoc]
  ... =
    (split_left a b).sum (Œªm c, c * œÄ x m.1 * œÄ y m.2) +
    (split_right a b).sum (Œªm c, c * œÄ x m.2 * œÄ y m.1) :
  begin
    rw [split_left, split_right, sum_map_domain_index]; simp [- not_lt],
    rw [‚Üê sum_add_index, filter_pos_add_filter_neg]; simp [add_mul],
    simp [add_mul]
  end
  ... =
    (split_left a b).curry.sum (Œªm f, œÄ x m * f.sum (Œªm c, c * œÄ y m)) +
    (split_right a b).curry.sum (Œªm f, œÄ y m * f.sum (Œªm c, c * œÄ x m)) :
  by
    rw [
      ‚Üê (sum_curry_index (split_left a b) (Œªm‚ÇÄ m‚ÇÅ c, c * œÄ x m‚ÇÄ * œÄ y m‚ÇÅ) _ _),
      ‚Üê (sum_curry_index (split_right a b) (Œªm‚ÇÄ m‚ÇÅ c, c * œÄ x m‚ÇÅ * œÄ y m‚ÇÄ) _ _)];
    simp [finsupp.mul_sum, mul_comm, mul_assoc, mul_left_comm, add_mul];
    simp [finsupp.sum, mul_comm, mul_assoc, mul_left_comm]

end

lemma sum_is_matrix_mul_left (a b : Œ±) (x y) :
  ((split_left a b).curry.sum (Œªm f, œÄ x m * f.sum (Œªm c, c * œÄ y m))) =
  (split_left a b).curry.sum (Œª m f, matrix.vec_mul_vec (Œª x : fin n ‚Üí Œ±, œÄ x m)
                                      (Œª y : fin n ‚Üí Œ±, f.sum (Œª m c, c * œÄ y m))) x y :=
begin
  rw ‚Üêfinsupp.sum_matrix _ _ x y,
  refl
end

lemma sum_is_matrix_mul_right (a b : Œ±) (x y) :
  ((split_right a b).curry.sum (Œªm f, œÄ y m * f.sum (Œªm c, c * œÄ x m))) =
  (split_right a b).curry.sum (Œª m f, matrix.vec_mul_vec (Œª x : fin n ‚Üí Œ±, f.sum (Œª m c, c * œÄ x m))
                               (Œª y : fin n ‚Üí Œ±, œÄ y m)) x y :=
begin
  rw ‚Üêfinsupp.sum_matrix _ _ x y,
  congr, ext, simp [matrix.vec_mul_vec, mul_comm]
end

lemma vmv_rank (m : multiset (fin n)) (f : multiset (fin n) ‚Üí‚ÇÄ Œ±) :
  rank (matrix.vec_mul_vec
    (Œª x : fin n ‚Üí Œ±, œÄ x m) (Œª y : fin n ‚Üí Œ±, f.sum (Œª m c, c * œÄ y m))).to_lin ‚â§ 1 :=
matrix.rank_vec_mul_vec _ _

def B : matrix {x // x ‚àà A} {x // x ‚àà A} Œ±
| x y := p.eval (a ‚Ä¢ x + b ‚Ä¢ y)

lemma B_entry_eq_sum (i j) : B i j =
  (split_left a b).curry.sum (Œªm f, œÄ i.val m * f.sum (Œªm c, c * œÄ j.val m)) +
  (split_right a b).curry.sum (Œªm f, œÄ j.val m * f.sum (Œªm c, c * œÄ i.val m)) :=
eval_add_eq_split_sum

lemma B_entry_eq_sum_matrix (i j) : B i j =
  (split_left a b).curry.sum (Œª m f, matrix.vec_mul_vec (Œª x, œÄ x m)
                                      (Œª y, f.sum (Œª m c, c * œÄ y m))) i.val j.val +
  (split_right a b).curry.sum (Œª m f, matrix.vec_mul_vec (Œª x, f.sum (Œª m c, c * œÄ x m))
                               (Œª y, œÄ y m)) i.val j.val :=
begin
  rw [B_entry_eq_sum, sum_is_matrix_mul_left, sum_is_matrix_mul_right]
end

lemma B_eq_sum_matrix : B =
  (split_left a b).curry.sum (Œª m f, matrix.vec_mul_vec (Œª x, œÄ x.val m)
                                      (Œª y, f.sum (Œª m c, c * œÄ y.val m))) +
  (split_right a b).curry.sum (Œª m f, matrix.vec_mul_vec (Œª x, f.sum (Œª m c, c * œÄ x.val m))
                               (Œª y, œÄ y.val m)) :=
begin
  ext, rw B_entry_eq_sum_matrix, congr;
  { rw [finsupp.sum_matrix, finsupp.sum_matrix], refl }
end

lemma B_diagonal_card : fintype.card {i : {x // x ‚àà A} // p.eval (-c ‚Ä¢ i.val) ‚â† 0} =
  (A.filter (Œª a, p.eval (-c ‚Ä¢ a) ‚â† 0)).card :=
suffices fintype.card { i // i ‚àà A ‚àß p.eval (-c ‚Ä¢ i) ‚â† 0} = (A.filter (Œª a, p.eval (-c ‚Ä¢ a) ‚â† 0)).card,
  begin
    rw ‚Üêthis,
    apply fintype.card_congr,
    refine {to_fun := Œª x, ‚ü®x.1.1, x.1.2, x.2‚ü©, inv_fun := Œª x, ‚ü®‚ü®x.1, x.2.1‚ü©, x.2.2‚ü©, ..},
    { simp [function.left_inverse] },
    { simp [function.right_inverse, function.left_inverse] }
  end,
fintype.card_of_subtype _ $ Œª a, finset.mem_filter

-- strategy: B = Œ£ M·µ¢ + Œ£ N·µ¢, where there are ‚â§ m (d/2) i's and rank(M·µ¢), rank(N·µ¢) ‚â§ 1
include d_nonneg
lemma B_rank_le : (rank B.to_lin) ‚â§ ‚Üë(2 * m (d/2)) :=
suffices rank B.to_lin ‚â§ ‚Üë(m (d/2)) + ‚Üë(m (d/2)), by simpa [two_mul],
begin
  rw [B_eq_sum_matrix, matrix.to_lin_add],
  transitivity,
  apply rank_add_le,
  apply cardinal.add_le_add;
  `[rw [finsupp.sum_matrix_to_lin],
    transitivity,
    apply rank_finset_sum_le,
    rw [‚Üêmul_one (m (d/2)), nat.cast_mul, nat.cast_one, ‚Üêadd_monoid.smul_eq_mul],
    transitivity add_monoid.smul _ (1 : cardinal),
    { apply finset.sum_le_card_mul_of_bdd,
      intros, apply matrix.rank_vec_mul_vec },
    apply add_monoid.smul_le_smul (cardinal.zero_le _)],
    apply split_left_curry_card, apply split_right_curry_card
end

include habc h
lemma B_diagonal : B = matrix.diagonal (Œª n, p.eval (-c ‚Ä¢ n.val)) :=
matrix.ext $ Œª i j,
  if hij : i = j then by rw [B, hij, ‚Üêadd_smul, a_plus_b]; simp; refl
  else begin
    rw [B, h],
    { simp [hij] },
    { exact i.2 },
    { exact j.2 },
    { intro h2, apply hij, exact subtype.eq h2 }
  end

lemma B_rank_eq :
  (rank B.to_lin).to_nat = (A.filter (Œªa : fin n ‚Üí Œ±, mv_polynomial.eval (- c ‚Ä¢ a) p ‚â† 0 )).card :=
begin
  rw [B_diagonal, matrix.rank_diagonal, ‚ÜêB_diagonal_card, cardinal.to_nat_coe],
  congr -- WHY??
end

theorem proposition_11_1 :
  (A.filter (Œªa : fin n ‚Üí Œ±, mv_polynomial.eval (- c ‚Ä¢ a) p ‚â† 0 )).card ‚â§ 2 * m (d / 2) :=
begin
  rw ‚ÜêB_rank_eq,
  transitivity,
  { apply cardinal.to_nat_le_of_le _,
    apply B_rank_le,
    apply cardinal.nat_lt_omega },
  { rw cardinal.to_nat_coe }
end

end Propostion_11_1

-- corresponds to section 12 of Sander's writeup
section Theorem4
set_option class.instance_max_depth 200
parameters {a b c : Œ±} (h_a_ne_zero : c ‚â† 0) (h_sum : a + b + c = 0) (h_n_pos : n > 0)
           {A : finset (fin n ‚Üí Œ±)}
           (h_A : ‚àÄ x y z ‚àà A, a‚Ä¢x + b‚Ä¢y + c‚Ä¢z = (0 : fin n ‚Üí Œ±) ‚Üí x = y ‚àß x = z)


section
parameters {d : ‚Ñö} (hd : d ‚â§ (q-1)*n) (d_nonneg : 0 ‚â§ d) -- q is the cardinality of Œ±

def neg_cA : finset (fin n ‚Üí Œ±) := A.image (Œª z, (-c) ‚Ä¢ z)

include h_a_ne_zero

lemma map_is_bijective : function.bijective (Œª x : fin n ‚Üí Œ±, -c‚Ä¢x) :=
equiv.bijective $ linear_equiv.to_equiv $ linear_equiv.smul_of_ne_zero (fin n ‚Üí Œ±) (-c) $
  by simp [h_a_ne_zero]

omit h_a_ne_zero

lemma neg_cA_card : neg_cA.card = A.card :=
finset.card_image_of_injective _ map_is_bijective.1

lemma A_card_le_Œ±_card_n : A.card ‚â§ fintype.card Œ±^n :=
calc A.card ‚â§ finset.univ.card : finset.card_le_of_subset (finset.subset_univ _)
        ... = fintype.card (fin n ‚Üí Œ±) : finset.card_univ
        ... = fintype.card Œ±^n : by simp

def V : set S := zero_set (S' d) (finset.univ \ neg_cA)

def V' : subspace Œ± S := zero_set_subspace (S' d) (finset.univ \ neg_cA)

lemma p_in_V_vanishes {p : S} : p ‚àà V ‚Üí
  ‚àÄ {x : fin n ‚Üí Œ±}, x ‚àâ neg_cA ‚Üí p.eval x = 0
| ‚ü®_, p_spec‚ü© _ h_xnmem := p_spec _ $ finset.mem_sdiff.2 ‚ü®finset.mem_univ _, h_xnmem‚ü©

lemma V_dim_finite : (vector_space.dim Œ± V') < cardinal.omega :=
zero_set_subspace_dim _ _ (dim_span_of_finset _)

noncomputable def V_dim := (vector_space.dim Œ± V').to_nat

include h_n_pos h_a_ne_zero

lemma diff_card_comp : (finset.univ \ neg_cA).card + A.card = q^n :=
by rw [finset.card_univ_diff, fintype.card_fin_arrow, neg_cA_card,
  nat.sub_add_cancel A_card_le_Œ±_card_n]; refl

theorem lemma_12_2 :  q^n + V_dim ‚â• m d + A.card :=
have V_dim + (finset.univ \ neg_cA).card ‚â• m d,
  from lemma_9_2 _ _ V_dim_finite,
by linarith using [diff_card_comp]

omit h_a_ne_zero

def sup (p : S) : finset (fin n ‚Üí Œ±) :=
finset.univ.filter $ Œª x, mv_polynomial.eval x p ‚â† 0

lemma mem_sup_iff {p : S} {x : fin n ‚Üí Œ±} : x ‚àà sup p ‚Üî p.eval x ‚â† 0 :=
by rw [sup, finset.mem_filter, and_iff_right (finset.mem_univ _)]

lemma mem_sup {p : S} {x : fin n ‚Üí Œ±} : p.eval x ‚â† 0 ‚Üí x ‚àà sup p := mem_sup_iff.2

lemma exi_max_sup : ‚àÉ P ‚àà V, ‚àÄ P' ‚àà V, sup P ‚äÜ sup P' ‚Üí sup P = sup P' :=
begin
  refine set.finite.exists_maximal_wrt sup _ _ _,
  { refine cardinal.lt_omega_iff_fintype.1 _,
    apply cardinal_lt_omega_of_dim_lt_omega V_dim_finite },
  exact set.ne_empty_iff_exists_mem.2 ‚ü®0, V'.zero_mem‚ü©
end

noncomputable def P := classical.some exi_max_sup

lemma P_V : P ‚àà V := have _ := classical.some_spec exi_max_sup, by tauto

lemma P_in_S' : P ‚àà S' d := P_V.1

lemma P_spec : ‚àÄ P' ‚àà V, sup P ‚äÜ sup P' ‚Üí sup P = sup P' :=
have _ := classical.some_spec exi_max_sup, by tauto

noncomputable def P_sig := sup P

lemma eval_ne_zero_of_mem_P_sig {x} (hx : x ‚àà P_sig) : P.eval x ‚â† 0 :=
(finset.mem_filter.1 hx).2

lemma eval_eq_zero_of_not_mem_neg_cA {x} (hx : x ‚àâ neg_cA) : P.eval x = 0 :=
P_V.2 _ $ (finset.mem_sdiff.2 ‚ü®finset.mem_univ _, hx‚ü©)

def W : set S := zero_set V' P_sig

lemma eval_mem_W_eq_zero_of_not_mem_neg_cA {Q} (hQ : Q ‚àà W) {x} (hx : x ‚àâ neg_cA) : Q.eval x = 0 :=
hQ.1.2 _ $ finset.mem_sdiff.2 ‚ü®finset.mem_univ _, hx‚ü©

noncomputable def W_dim := (vector_space.dim Œ± (zero_set_subspace V' P_sig)).to_nat

lemma W_dim_ge : W_dim + P_sig.card ‚â• V_dim :=
lemma_9_2 V' P_sig (zero_set_subspace_dim _ _ V_dim_finite)

theorem lemma_12_3 : P_sig.card ‚â• V_dim :=
le_of_not_gt $
assume hlt : P_sig.card < V_dim,
have W_dim > 0, by linarith using [W_dim_ge],
let ‚ü®Q, hQW, hQ‚ü© := exists_mem_ne_zero_of_dim_pos (cardinal.pos_of_to_nat_pos this) in
have hPQ : ‚àÄ x ‚àà P_sig, (P + Q).eval x ‚â† 0, from
  assume x hxP_sig,
  have hxP : P.eval x ‚â† 0, from eval_ne_zero_of_mem_P_sig hxP_sig,
  have hxQ : Q.eval x = 0, from hQW.2 _ hxP_sig,
  by simp [hxP, hxQ],
have hQq : Q ‚àà restrict_degree (fin n) Œ± (q - 1) :=
begin
  refine S'_le_restrict_q _ _, -- if instantiated with `d` and `hQW.1.1` it doesn't terminate!
  exact d,
  exact hQW.1.1,
end,
have ‚àÉ y, Q.eval y ‚â† 0, from
  not_forall.1 $ mt (assume h, Q.eq_zero_of_eval_eq_zero _ _ h hQq) hQ,
let ‚ü®y, hQy‚ü© := this in
have hPsig_y : y ‚àâ P_sig, from Œª h, hQy $ hQW.2 _ h,
have hPy : P.eval y = 0, from by_contradiction $ Œª h, hPsig_y (mem_sup h),
have (P + Q).eval y ‚â† 0, by simp [hPy, hQy],
have hPQsup_y : y ‚àà sup (P + Q), from mem_sup this,
have hxPQ : ‚àÄ x, x ‚àà finset.univ \ neg_cA ‚Üí (P + Q).eval x = 0, from
  Œª _ hx, by cases finset.mem_sdiff.1 hx with _ hx;
             simp [eval_eq_zero_of_not_mem_neg_cA hx, eval_mem_W_eq_zero_of_not_mem_neg_cA hQW hx],
have hmemS' : P + Q ‚àà (S' d), from submodule.add_mem _ P_V.1 hQW.1.1,
have hPQV : (P + Q) ‚àà V, from ‚ü®hmemS', hxPQ‚ü©,
have hSup : P_sig = sup (P + Q), from P_spec _ hPQV $ Œª x hx, mem_sup (hPQ _ hx),
hPsig_y $ by rwa ‚ÜêhSup at hPQsup_y

def A_antidiag : finset ((fin n ‚Üí Œ±) √ó (fin n ‚Üí Œ±)) :=
(A.product A).filter (Œª p, p.1 ‚â† p.2)

def abS : finset (fin n ‚Üí Œ±) := A_antidiag.image $ Œª p, a‚Ä¢p.1 + b‚Ä¢p.2

include h_A
lemma inter_empty : abS ‚à© neg_cA = ‚àÖ :=
begin
  apply finset.eq_empty_of_forall_not_mem,
  intros x h_xmem,
  rcases finset.mem_image.1 (finset.mem_of_mem_inter_left h_xmem) with ‚ü®‚ü®l, r‚ü©, hlr, hx_sum‚ü©,
  rcases finset.mem_image.1 (finset.mem_of_mem_inter_right h_xmem) with ‚ü®w, hw, hx_w‚ü©,
  cases finset.mem_filter.1 hlr with hp h_lrne,
  cases finset.mem_product.1 hp with h_la h_ra,
  dsimp at hx_sum,
  have : a‚Ä¢l + b‚Ä¢r +c‚Ä¢w = 0, {rw [hx_sum, ‚Üêhx_w], simp},
  apply h_lrne,
  refine (h_A _ _ _ _ _ _ this).left; assumption
end

lemma P_vanishes_off_A_image (x : fin n ‚Üí Œ±) (h_xmemS : x ‚àâ neg_cA) :
  mv_polynomial.eval x P = 0 :=
p_in_V_vanishes P_V h_xmemS

lemma P_vanishes_on_abS (x : fin n ‚Üí Œ±) (h_xmemS : x ‚àà abS) : mv_polynomial.eval x P = 0 :=
begin
  apply P_vanishes_off_A_image,
  intro h_xmemimage,
  apply finset.not_mem_empty x,
  rw ‚Üêinter_empty,
  apply finset.mem_inter_of_mem; assumption
end

lemma filter_eq : finset.univ.filter (Œª x : fin n ‚Üí Œ±, mv_polynomial.eval x P ‚â† 0) =
  finset.filter (Œª x, mv_polynomial.eval x P ‚â† 0) neg_cA :=
finset.ext.2 $ Œª x,
‚ü® Œª h_xmem,
  let ‚ü®_, h_xeval‚ü© := finset.mem_filter.1 h_xmem in
  finset.mem_filter.2 ‚ü®have _, from P_vanishes_off_A_image x, by clear_aux_decl; tauto, h_xeval‚ü©,
  Œª h_xmem, finset.mem_filter.2 ‚ü®finset.mem_univ _, (finset.mem_filter.1 h_xmem).2‚ü© ‚ü©

theorem lemma_12_4 : P_sig.card ‚â§ 2 * m (d/2) :=
have h_P_vanishes' : ‚àÄ x, x ‚àà A ‚Üí ‚àÄ y, y ‚àà A ‚Üí x ‚â† y ‚Üí mv_polynomial.eval (a‚Ä¢x + b‚Ä¢y) P = 0,
begin
  intros x hx y hy hxy,
  apply P_vanishes_on_abS (a‚Ä¢x + b‚Ä¢y),
  apply finset.mem_image.2,
  have : (x, y) ‚àà A_antidiag, from finset.mem_filter.2 ‚ü®finset.mem_product.2 ‚ü®hx, hy‚ü©, hxy‚ü©,
  apply exists.intro,
  existsi this,
  refl
end,
have h_bound : _ := proposition_11_1 d_nonneg A a b c P_in_S' h_sum h_P_vanishes',
have h_P_sig_def : P_sig = finset.image (Œª z, (-c) ‚Ä¢ z) (A.filter (Œª x, P.eval (-c‚Ä¢x) ‚â† 0)), from calc
    P_sig = finset.univ.filter (Œª x, P.eval x ‚â† 0) : rfl
      ... = finset.filter (Œª x, P.eval x ‚â† 0) neg_cA : filter_eq
      ... = finset.image (Œª z, (-c) ‚Ä¢ z) (A.filter (Œª x, P.eval (-c‚Ä¢x) ‚â† 0)) : finset.image_filter,
have h_P_card : P_sig.card = (A.filter (Œª x, P.eval (-c‚Ä¢x) ‚â† 0)).card, from calc
    P_sig.card = (finset.image (Œª z, (-c) ‚Ä¢ z) (A.filter (Œª a, P.eval (-c‚Ä¢a) ‚â† 0))).card : by rw h_P_sig_def
           ... = ((A.filter (Œª x, P.eval (-c‚Ä¢x) ‚â† 0))).card : finset.card_image_of_injective _ map_is_bijective.1,
by rwa ‚Üêh_P_card at h_bound

omit h_A

section
omit h_n_pos

def l125_A : finset (fin n ‚Üí fin q) := finset.univ

@[reducible] def monom (v : fin n ‚Üí fin q) : mv_polynomial (fin n) Œ± := monomial v 1

lemma total_degree_monom (v : fin n ‚Üí fin q) : total_degree (monom v) = fin.sum (v : fin n ‚Üí ‚Ñï)  :=
by simp only [monom, monomial_total_degree, fin_sum_finsupp_sum]; refl

def l125_B := l125_A.filter $ Œª v, ‚Üë(total_degree (monom v)) ‚â§ d
def l125_C := l125_A.filter $ Œª v, ‚Üë(total_degree (monom v)) > d
def l125_D := l125_A.filter $ Œª v, ‚Üë(total_degree (monom v)) < ((q : ‚Ñö) - 1)*n - d
def l125_E := l125_A.filter $ Œª v, ‚Üë(total_degree (monom v)) ‚â§ ((q : ‚Ñö) - 1)*n - d

lemma monom_exps_M'_card (k : ‚Ñö) : ((M' k).image monom_exps).card = (M' k).card :=
finset.card_image_of_inj_on $
  Œª v1 hv1 v2 hv2 hv, monom_exps_inj (finset.mem_filter.1 hv1).1 (finset.mem_filter.1 hv2).1 hv

lemma monom_exps_M'_card_m (k : ‚Ñö) : ((M' k).image monom_exps).card = m k :=
by rw [monom_exps_M'_card, M'_card]

lemma B_C_disjoint : disjoint l125_B l125_C :=
Œª x hx, by linarith using [(finset.mem_filter.1 (finset.mem_of_mem_inter_left hx)).2,
                           (finset.mem_filter.1 (finset.mem_of_mem_inter_right hx)).2]

lemma B_C_union : l125_B ‚à™ l125_C = l125_A :=
finset.ext.2 $ Œª x,
‚ü® Œª _, finset.mem_univ _,
  Œª _, finset.mem_union.2 $
    if hx : ‚Üë(total_degree (monom x)) ‚â§ d then or.inl (finset.mem_filter.2 ‚ü®finset.mem_univ _, hx‚ü©)
    else or.inr (finset.mem_filter.2 ‚ü®finset.mem_univ _, lt_of_not_ge hx‚ü©) ‚ü©

lemma filter_eq_image (r : ‚Ñö) :
  (l125_A.filter $ Œª v, ‚Üë(total_degree (monom v)) ‚â§ r) = (M' r).image monom_exps :=
finset.ext.2 $ Œª x,
‚ü® Œª h, finset.mem_image.2
  ‚ü® monomial x 1, finset.mem_filter.2 ‚ü®monomial_mem_M _, (finset.mem_filter.1 h).2‚ü©, by simp ‚ü©,
  Œª h, finset.mem_filter.2
  ‚ü® finset.mem_univ _,
    let ‚ü®a, ha, hax‚ü© := finset.mem_image.1 h,
        ha' := (finset.mem_filter.1 ha).2 in
    by rw [‚Üêhax, monom, monom_exps_def (mem_M_of_mem_M' ha)]; exact ha' ‚ü©‚ü©

lemma le_helper (v : ‚Ñï) : q - (v + 1) < q := nat.sub_lt one_le_q (nat.succ_pos _)

def invol (v : fin n ‚Üí fin q) : fin n ‚Üí fin q :=
Œª x, ‚ü®q - (v x + 1), le_helper _‚ü©

lemma invol_add (v i) : ((invol v) i : ‚Ñï) + v i = q - 1 :=
show q - (‚Üë(v i) + 1) + ‚Üë(v i) = q - 1,
begin
  rw ‚Üênat.sub_add_comm,
  { apply nat.sub_eq_of_eq_add,
    rw [add_assoc _ 1, nat.add_sub_cancel'],
    { ac_refl },
    { exact one_le_q } },
  { apply nat.succ_le_of_lt,
    apply fin.is_lt }
end

-- not sure why it appears like this below
lemma invol_add' (v i) : (‚áë(invol v) : fin n ‚Üí ‚Ñï) i + (‚áëv : fin n ‚Üí ‚Ñï) i = q - 1 :=
invol_add _ _

lemma invol_is_involution (x : fin n ‚Üí fin q) : invol (invol x) = x :=
funext $ Œª i, (fin.ext_iff _ _).2 $
  show q - (q - (x i + 1) + 1) = x i,
  begin
    rw [‚Üênat.sub_sub, ‚Üênat.sub_sub, nat.sub_sub, nat.sub_add_cancel, nat.sub_sub_self],
    { apply le_of_lt, apply fin.is_lt },
    { apply (nat.add_le_to_le_sub _ _).1,
      { rw add_comm, apply fin.is_lt },
      { apply le_of_lt, apply fin.is_lt }}
  end

lemma invol_inj : function.injective invol :=
Œª v1 v2 hv, by rw [‚Üêinvol_is_involution v1, ‚Üêinvol_is_involution v2, hv]

lemma total_degree_invol_eq (v : fin n ‚Üí fin q) :
   ‚Üë(total_degree (monom (invol v))) = ((q : ‚Ñö)-1)*n - ‚Üë(total_degree (monom v)) :=
begin
  simp only [total_degree_monom],
  apply eq_sub_of_add_eq,
  rw [‚Üênat.cast_add, fin.sum_add],
  simp only [invol_add'],
  have qcast: ((q : ‚Ñö) - 1) * n = ‚Üë((q - 1)*n), {simp [nat.cast_sub one_le_q]},
  rw [qcast, mul_comm], congr, -- something funny with the casts here
  convert fin.sum_const _ _,
  simp
end

lemma invol_total_degree (v : fin n ‚Üí fin q) :
  ‚Üë(total_degree (monom v)) > d ‚Üî ‚Üë(total_degree (monom (invol v))) < ((q : ‚Ñö) - 1)*n - d :=
by constructor; { rw total_degree_invol_eq, intro, linarith }

lemma D_eq_C_image_invol : l125_D = l125_C.image invol :=
finset.ext.2 $ Œª v,
  ‚ü® Œª h, finset.mem_image.2
     ‚ü®invol v, finset.mem_filter.2 ‚ü®finset.mem_univ _,
       by rw [invol_total_degree, invol_is_involution];
          apply (finset.mem_filter.1 h).2‚ü©, invol_is_involution _‚ü©,
    Œª h, finset.mem_filter.2 ‚ü®finset.mem_univ _,
      begin
        rcases finset.mem_image.1 h with ‚ü®v', hv', hvv'‚ü©,
        rw [‚Üêhvv', ‚Üêinvol_total_degree],
        apply (finset.mem_filter.1 hv').2
      end‚ü©‚ü©

-- this is useful in asymptotics
parameters (Œ± n d)
lemma h_B_card : l125_B.card = m d :=
by rw [l125_B, filter_eq_image, monom_exps_M'_card_m]
parameters {Œ± n d}

include h_n_pos
theorem lemma_12_5 : q^n ‚â§ m (((q : ‚Ñö) - 1)*n - d) + m d :=
have h_A_card : l125_A.card = q^n, by simp [finset.card_univ, l125_A],
have h_B_card : l125_B.card = m d, by rw [l125_B, filter_eq_image, monom_exps_M'_card_m],
have h_B_C_card : l125_B.card + l125_C.card = l125_A.card,
  by rw [‚Üêfinset.card_disjoint_union B_C_disjoint, B_C_union],
have h_C_D_card : l125_C.card = l125_D.card,
  by rw [‚Üêfinset.card_image_of_injective _ invol_inj, D_eq_C_image_invol],
have h_D_E_card : l125_D.card ‚â§ l125_E.card, from
  finset.card_le_card_of_inj_on id
    (Œª a ha, finset.mem_filter.2 ‚ü®finset.mem_univ _, le_of_lt ((finset.mem_filter.1 ha).2)‚ü©)
    (Œª _ _ _ _, id),
have h_E_card : l125_E.card = m ((q-1)*n - d), by rw [l125_E, filter_eq_image, monom_exps_M'_card_m],
by linarith

include h_A hd h_a_ne_zero h_sum d_nonneg
theorem lemma_12_6 : A.card ‚â§ 2 * m (d/2) + m ((q - 1)*n - d) :=
by linarith using [lemma_12_2, lemma_12_3, lemma_12_4, lemma_12_5]

end

end

include h_A h_n_pos h_a_ne_zero h_sum
theorem theorem_12_1 : A.card ‚â§ 3*(m (1/3*((q-1)*n))) :=
begin
  set qn := ((q : ‚Ñö) - 1)*n with hqn,
  set! d := 2/3*qn with hd,
  have : qn ‚â• 0, from mul_nonneg (sub_nonneg_of_le one_le_q_real) (nat.cast_nonneg _),
  have : d ‚â§ qn, {linarith},
  have h_d2 : d / 2 = 1/3 * qn, {linarith},
  have h_qnd : qn - d = 1/3 * qn, {linarith},
  have h_l5, from lemma_12_6 this,
  rw [h_d2, h_qnd] at h_l5,
  convert h_l5 _,
  { ring },
  { apply mul_nonneg,
    { norm_num },
    { assumption } }
end

end Theorem4
end